规则引擎解释器规格 { .text-center }
---------------

&nbsp;

### 规则引擎中的脚本解释器

为实现与 AI 交互过程的灵活定制，TMY 规则引擎引入了虚拟机（Virtual Machine, VM）运行环境，让驱动机器人的各类接口均由虚拟机辅助加以控制。比如在 chatGPT 聊天室中用户最终调用 GPT 接口的各项参数，包括 prompt 提示语，temperature、max_tokens 等参数，均由脚本经动态计算获得。

当前版本的 VM 在表达式（expression）级别提供脚本解释执行机制，其脚本语法与 python 一致，是 python 语法的一个子集。

为了让大家快速理解脚本解释器的语法，下面我们提供一些例子（以下每行代码均符合我们虚拟机的 expression 规格要求）。

1. 使用字面文法描述常量

``` python
3
3.5
True
"example"
'for example'
```

2. 使用变量

``` python
IS_CN
where
```

3. 使用复合数据

``` python
(3,4)
[3,4]
[3,4,"ab"]
{"ab",5}
{"name":'wayne','readonly':IS_CN}
```

4. 运算

``` python
3 + 4
-(3 + 4)
not (3 > 4)
3 // 4
0x01 | 0x02
(0x01 | 0x02) == 0x03
3 in [3,4] and IS_CN
"cd" not in [3,4,"ab"]
"请介绍 " + where
3 if IS_CN else 4
```

&nbsp;

### 语法规则

结合 EBNF 与 PEG 混合记法，下面我们定义 TMY 规则引擎的 expression 规格。

```
expression:
    | disjunction 'if' disjunction 'else' expression 
    | disjunction

disjunction:
    | conjunction ('or' conjunction )+ 
    | conjunction

conjunction:
    | inversion ('and' inversion )+ 
    | inversion

inversion:
    | 'not' inversion 
    | comparison

comparison:
    | bitwise_or compare_op_bitwise_or_pair+ 
    | bitwise_or

compare_op_bitwise_or_pair:
    | eq_bitwise_or
    | noteq_bitwise_or
    | lte_bitwise_or
    | lt_bitwise_or
    | gte_bitwise_or
    | gt_bitwise_or
    | notin_bitwise_or
    | in_bitwise_or

eq_bitwise_or: '==' bitwise_or 
noteq_bitwise_or: '!=' bitwise_or 
lte_bitwise_or: '<=' bitwise_or 
lt_bitwise_or: '<' bitwise_or 
gte_bitwise_or: '>=' bitwise_or 
gt_bitwise_or: '>' bitwise_or 
notin_bitwise_or: 'not' 'in' bitwise_or 
in_bitwise_or: 'in' bitwise_or 

bitwise_or:
    | bitwise_or '|' bitwise_xor 
    | bitwise_xor

bitwise_xor:
    | bitwise_xor '^' bitwise_and 
    | bitwise_and

bitwise_and:
    | bitwise_and '&' sum 
    | sum

sum:
    | sum '+' term 
    | sum '-' term 
    | term

term:
    | term '*' factor 
    | term '/' factor 
    | term '//' factor 
    | term '%' factor 
    | factor

factor:
    | '-' factor 
    | atom

atom:
    | NAME
    | 'True'
    | 'False'
    | strings
    | NUMBER
    | (tuple)
    | (list)
    | (dict | set)
```

说明：上述定义中 atom 下各项定义，与 python 中对应的定义完全相同，所以我们不再展开。
